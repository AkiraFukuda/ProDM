cmake_minimum_required(VERSION 3.15)
project(ProDM VERSION 0.1 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

include(GNUInstallDirs)
include(CMakePackageConfigHelpers)

# ---- deps ----
find_library(ZSTD_LIB zstd HINTS "${CMAKE_CURRENT_SOURCE_DIR}/external/SZ/install/${CMAKE_INSTALL_LIBDIR}")
find_library(SZ3_LIB  SZ3c HINTS "${CMAKE_CURRENT_SOURCE_DIR}/external/SZ3/install/lib")

set(ZSTD_INCLUDES  "${CMAKE_CURRENT_SOURCE_DIR}/external/SZ3/tools/zstd")
set(SZ3_INCLUDES   "${CMAKE_CURRENT_SOURCE_DIR}/external/SZ3/install/include")
set(MGARDx_INCLUDES "${CMAKE_CURRENT_SOURCE_DIR}/external/MGARDx/install/include")

# ---- target ----
add_library(ProDM INTERFACE)
add_library(ProDM::ProDM ALIAS ProDM)

target_include_directories(ProDM
  INTERFACE
    $<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}/include>
    $<BUILD_INTERFACE:${SZ3_INCLUDES}>
    $<BUILD_INTERFACE:${ZSTD_INCLUDES}>
    $<BUILD_INTERFACE:${MGARDx_INCLUDES}>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
)

target_link_libraries(ProDM
  INTERFACE
    ${ZSTD_LIB}
    ${SZ3_LIB}
)

# ---- install headers ----
install(DIRECTORY ${PROJECT_SOURCE_DIR}/include/ DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})

# ---- install/export targets ----
install(TARGETS ProDM
  EXPORT ProDMTargets
)

install(EXPORT ProDMTargets
  NAMESPACE ProDM::
  DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/ProDM
)

# ---- generate & install ProDMConfig.cmake / Version ----
configure_package_config_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/cmake/ProDMConfig.cmake.in
  ${CMAKE_CURRENT_BINARY_DIR}/ProDMConfig.cmake
  INSTALL_DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/ProDM
)

write_basic_package_version_file(
  ${CMAKE_CURRENT_BINARY_DIR}/ProDMConfigVersion.cmake
  VERSION ${PROJECT_VERSION}
  COMPATIBILITY SameMajorVersion
)

install(FILES
  ${CMAKE_CURRENT_BINARY_DIR}/ProDMConfig.cmake
  ${CMAKE_CURRENT_BINARY_DIR}/ProDMConfigVersion.cmake
  DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/ProDM
)

add_subdirectory(test)
